' Gambas class file

Private manejadorPersona As New PersonaDAO

Public Sub Form_Open()
  
  Dim ctmp As New Conexion
  
  If Not Exist(Ctmp.NombreBaseDatos) Then 
    Copy "./EstructuraBaseDatos/lecturas.sqlite" To Ctmp.NombreBaseDatos
    
  Endif
  
  Me.Center
  
  'cargar Niveles y Grado
  ComboBoxNivelGrado.list = ModuleComun.NivelesGrado()
  'la clase se debe de cargar
  ComboBoxClase.list = ModuleComun.Clases()
  
  ' Por defecto será un alumno
  ComboBoxTipoProfesorAlumnos.tag = "Alumno" 
  
End

Public Sub ButtonCrear_Click()
  
  Dim passwordProfesor As String
  Dim PersonaVOtmp As New PersonaVO
  
  If TextBoxContrasena.text = TextBoxRepiteContrasena.text Then 
    
    'crea alumno o profesor
    'añade el registro
    PersonaVOtmp.nombre = TextBoxNombre.Text
    PersonaVOtmp.password = TextBoxContrasena.Text
    PersonaVOtmp.tipo = ComboBoxTipoProfesorAlumnos.tag 
    
    If ComboBoxTipoProfesorAlumnos.tag = "Alumno" Or ComboBoxTipoProfesorAlumnos.tag = "" Then 
      'es una alumno
      PersonaVOtmp.clase = ComboBoxClase.Text
      PersonaVOtmp.nivel = ComboBoxNivelGrado.Text
      
    Else
      
      passwordProfesor = InputBox(("Contraseña:"), "Contraseña Maestra")
      
      If passwordProfesor <> Settings["ContraseñaMaestra", "MININO"] Then 
        Message.Error(("Error: no se ha introducido bien la contraseña maestra"))
        Return 
        
      Endif
      '      
      
    Endif
    
    If manejadorPersona.registrar(PersonaVOtmp) Then
      Message.Info(("Se ha creado el registro"))
      Me.Close()
    Else
      Message.Info(("Ha habido algun problema al crear el registro"))
    Endif
  Else
    Message.Info(("Error: las contraseñas no son iguales"))
  Endif
  
End

Public Sub ButtonCancelar_Click()
  
  'cancela la insercion de nueva persona
  Me.Close()
  
End

Public Sub ComboBoxTipoProfesorAlumnos_Leave()
  
  If ComboBoxTipoProfesorAlumnos.text = ("Profesor") Then 
    ComboBoxNivelGrado.enabled = False
    ComboBoxClase.Enabled = False
  Else
    ComboBoxNivelGrado.enabled = True
    ComboBoxClase.Enabled = True
  Endif
  
End

Public Sub TextBoxNombre_Leave()
  
  'poner en mayusculas los nombres 
  TextBoxNombre.text = String.Upper$(TextBoxNombre.text)
  
End

Public Sub TextBoxRepiteContrasena_LostFocus()
  
  If TextBoxContrasena.text = TextBoxRepiteContrasena.text Then 
    
    PictureBoxComprobarContrasena.Picture = Picture["icon:/22/apply"]
    
  Else
    
    PictureBoxComprobarContrasena.Picture = Picture["icon:/22/error"]
  Endif  
  
End

Public Sub ComboBoxTipoProfesorAlumnos_LostFocus()
  'primer elemento es Alumno
  'el segundo elemento es Profesor
  
  Dim tipo As Integer
  
  tipo = ComboBoxTipoProfesorAlumnos.List.Find(ComboBoxTipoProfesorAlumnos.Text)
  
  If tipo = 0 Then 
    ComboBoxTipoProfesorAlumnos.tag = "Alumno" 'Funciona como una banderan, se usa internamente en el programa y no hay que traducirla
    Return 
    
  Endif
  
  If tipo = 1 Then 
    ComboBoxTipoProfesorAlumnos.tag = "Profesor" 'Funciona como una banderan, se usa internamente en el programa y no hay que traducirla
    Return 
    
  Endif
  
End
