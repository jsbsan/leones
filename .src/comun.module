' Gambas module file

Public Function extraeColumnaArray(datos As Variant[], nombrecampo As String) As String
  
  Dim datotmp As Object
  Dim lista As New String[]
  
  For Each datotmp In datos
    lista.Add(Object.GetProperty(datotmp, nombrecampo))
  Next
  
  Return lista.Join(",")
  
End

'-------------------------------------------------------------------------------------------------
'funcion generica para extraer dos columnas (como colecciones, key="compuesto" y key="Id")
'y devuelve valores separados por coma
'-------------------------------------------------------------------------------------------------

Public Function ExtraerLista(nombreVista As String, nombreCampo As String, nombreId As String) As Collection
  
  Dim devolver As New Collection
  Dim hre As Result
  
  Dim manejadorTemporalDAO As Object
  Dim datostemporalVO As Object[]
  
  manejadorTemporalDAO = Object.New(nombreVista & "DAO")
  datostemporalVO = Object.New(nombreVista & "VO[]")
  
  hre = manejadorTemporalDAO.contenido() 'obtener todos los registros
  datostemporalVO = manejadorTemporalDAO.ConvertirResult(hre) 'convierto los resultados en un array de clases Vista objeto
  
  devolver.add(manejadorTemporalDAO.extraeColumnaArray(datostemporalVO, nombreCampo), "Compuesto")
  devolver.add(manejadorTemporalDAO.extraeColumnaArray(datostemporalVO, nombreId), "Id")
  
  Return devolver
  
End

'----------------------------------------------
Public Sub cambiarFecha(f As String) As String
  
  Dim anno As String
  Dim dia As String
  Dim mes As String
  
  ''NOTE: Cuidado con el formato de datos que tiene segun el pais
  mes = Mid$(f, 1, 2)
  dia = Mid$(f, 4, 2)
  anno = Mid$(f, 7, 4)
  
  Return anno & "-" & mes & "-" & dia
  
End

Public Sub MensajeError()
  
  Message.error(Error.Where & "\n" & error.Text & "\n" & ("Trazada:") & "\n" & Error.Backtrace.Join("\n"))
  
End

Public Sub rellenalista(ListBoxPersonas As ListBox)
  
  Dim texto As String
  Dim resultado As Result
  Dim PersonaDAOtmp As New PersonaDAO
  
  ListBoxPersonas.Clear()
  
  resultado = PersonaDAOtmp.sql("Select idpersona,nombre,nivel,clase,tipo from Persona Order By UPPER(clase),UPPER(nivel),UPPER(nombre)")
  
  While resultado.Available
    texto = "Clase: " & resultado["clase"] & " | " & resultado["nivel"] & " | " & resultado["nombre"]
    ListBoxPersonas.Add(texto)
    resultado.MoveNext
  Wend
  
End

''informe
Public Sub InformeAlumno(PersonaVOtmp As PersonaVO)
  
  Dim informe As String
  Dim sqlString As String
  Dim media As Integer
  Dim resultado As Result
  Dim SumaVelocidad As Integer
  Dim VELOCIDAD As Integer
  Dim TABLA As String
  Dim PruebasDAOtmp As New PruebasDAO
  
  informe = File.Load("html/plantilla01.html")
  
  informe = Replace$(informe, "##FECHA##", Format$(Now, "dddd") & " " & Format(Now, "dd") & " de " & Format(Now, "mmmm") & " de " & Format(Now, "yyyy"))
  informe = Replace$(informe, "##HORAS##", Format$(Now, "hh:nn:ss"))
  
  informe = Replace$(informe, "##ALUMNO##", PersonaVOtmp.nombre)
  
  informe = Replace$(informe, "##CLASE##", PersonaVOtmp.clase)
  
  informe = Replace$(informe, "##NIVEL##", PersonaVOtmp.nivel)
  
  sqlString = "Select fecha,puntuacion,textolectura from Pruebas where" 
  sqlString &= " idpersona=" & PersonaVOtmp.idpersona & " " 'alumno que nos pasan de referencia
  sqlString &= " Order by fecha" 'ordenado por fechas
  resultado = PruebasDAOtmp.sql(sqlString)
  
  'con esto consigo un listado fecha y puntuacion y lecturas
  'CALCULAR
  'reinicio la velocidad
  SumaVelocidad = 0
  
  While resultado.Available
    velocidad = resultado["puntuacion"]
    SumaVelocidad += velocidad
    resultado.MoveNext
  Wend
  
  media = SumaVelocidad / resultado.Max
  
  'Media de palabras por minuto del alumno: ##MEDIA ##(seg ún su nivel, debe de estar comprendido entre ##MINIMO## y ##MAXIMO###) <br/>
  
  informe = Replace$(informe, "##MEDIA##", Str$(media))
  
  informe = Replace$(informe, "##MINIMO##", Str$(ValorMinimoCurso(PersonaVOtmp.nivel)))
  
  informe = Replace$(informe, "##MAXIMO##", Str$(ValorMaximoCurso(PersonaVOtmp.nivel)))
  
  ''TABLA DE RESULTADOS
  TABLA = "<TABLE><tr>"
  TABLA &= "<th>" & ("Lectura") & "</th>"
  TABLA &= "<th>" & ("Palabras por Minuto") & "</th>"
  TABLA &= "<th>" & ("Fecha") & "</th>"
  TABLA &= "</tr>"
  
  resultado.MoveFirst
  
  While resultado.Available
    TABLA &= "<tr>"
    TABLA &= "<th>" & resultado["textolectura"] & "</th>"
    TABLA &= "<th>" & resultado["puntuacion"] & "</th>"
    TABLA &= "<th>" & resultado["fecha"] & "</th>"
    TABLA &= "</tr>"
    resultado.MoveNext
  Wend
  
  TABLA &= "</table>"
  
  informe = Replace$(informe, "##TABLA##", tabla)
  
  File.Save("/tmp/informe.html", informe)
  
  Desktop.Open("/tmp/informe.html")
  
End

Public Function ValorMaximoCurso(curso As String) As Integer
  
  Select Case Curso
    Case "Primaria 1º"
      Return 59
    Case "Primaria 2º"
      Return 84
    Case "Primaria 3º"
      Return 99
    Case "Primaria 4º"
      Return 114
    Case "Primaria 5º"
      Return 124
    Case "Primaria 6º"
      Return 134
    Case "Secundaria 1º"
      Return 144
    Case "Secundaria 2º"
      Return 154
    Case "Secundaria 3º"
      Return 160
  End Select
  
End

Public Function ValorMinimoCurso(curso As String) As Integer
  
  Select Case Curso
    Case "Primaria 1º"
      Return 35
    Case "Primaria 2º"
      Return 60
    Case "Primaria 3º"
      Return 85
    Case "Primaria 4º"
      Return 100
    Case "Primaria 5º"
      Return 115
    Case "Primaria 6º"
      Return 125
    Case "Secundaria 1º"
      Return 135
    Case "Secundaria 2º"
      Return 145
    Case "Secundaria 3º"
      Return 155
  End Select
  
End
