' Gambas class file

Public PersonaVOtmp As PersonaVO
Public ficheroNombreLectura As String
Public ficheroTexto As String 'texto del fichero seleccionado....que me lo tiene que pasar el formulario anterior...
Public tiempoPrueba As Integer
Public modo As String ''Modo Pruebas-> no graba la información, Modo Real -> graba la informacion

Private SumandoErrores As Integer = 0
Private cuentaAtras As Integer
Private tiempoPruebaInicial As Integer
Private TiempoFinal As Integer
Private pruebafinalizada As Boolean

Public Sub Form_Open()
  
  iniciaFormulario()
  
End

Public Sub iniciaFormulario()
  
  PanelPrincipal.Visible = False
  LabelCuentaAtras.text = Settings["cuantaAtras", 5]
  LabelCuentaAtras.Visible = True
  Me.Center()
  
  'leer datos de la configuracion...
  
  'leo configuracion...
  
  TextArea1.Background = Settings["colorfondo", Color.Black] 
  TextArea1.Foreground = Settings["colorletra", Color.White]
  TextArea1.font = Font[Settings["TipoLetra", "Serif,+8"]]
  
  TextArea1.text = ficheroTexto 'que me pases el texto... leñe!!!
  
  TextArea1.pos = 1 'me pongo al inicio del documento
  
  tiempoPrueba = Settings["tiempo", 90] 
  
  tiempoPruebaInicial = tiempoPrueba
  LabelTiempo.text = tiempoPrueba
  
  Timer1.Delay = 1000
  iniciaCuentaAtras()
  pruebafinalizada = False 'no se ha terminado la prueba
  
  'poner el foco en el TextArea
  TextArea1.SetFocus()
  
End

Public Sub Timer1_Timer()
  'poner el foco en el TextArea
  
  TextArea1.SetFocus()
  
  tiempoPrueba -= 1
  
  LabelTiempo.text = tiempoPrueba
  
  If tiempoPrueba <= 0 Then 
    pruebafinalizada = True
    Timer1.Stop()
    Message.Info("Seleccione palabra donde se quedo en la lectura")
    
  Endif
  
End

Public Sub Form_KeyRelease()
  
  If Key.code = Key.enter Or Key.code = Key.Return Then 
    SumandoErrores += 1
    
    Print SumandoErrores
    Stop Event
  Endif
  
End

Public Sub iniciaCuentaAtras()
  
  cuentaAtras = Settings["cuantaAtras", 5]
  TimerCuentaAtrasInicial.Start()
  
End

Public Sub TimerCuentaAtrasInicial_Timer()
  
  cuentaAtras -= 1
  LabelCuentaAtras.text = Str$(cuentaAtras)
  If cuentaAtras = -1 Then 
    LabelCuentaAtras.Visible = False
    PanelPrincipal.Visible = True
    
    LabelCuentaAtras.Visible = False
    
    Timer1.Start()
    TimerCuentaAtrasInicial.Stop()
  Endif
  
End

Public Sub ButtonParada_Click() ''parada forzada por profesor. El alumno termino la lectura antes de que acabase el tiempo
  
  pruebafinalizada = True 'he finalizado la prueba.... pedir palabra y pasar al calculo!!!
  
  TiempoFinal = tiempoPruebaInicial - tiempoPrueba
  Timer1.Stop()
  
  Message.Info(("Seleccione palabra donde se quedo en la lectura"))
  
End

Public Sub TextArea1_DblClick()
  
  Dim numeropalabras As Integer
  Dim textoAcontar As String
  Dim misDatosPruebas As New ClassDatosPruebas
  
  If pruebafinalizada = True Then 
    
    numeropalabras = ModuleComun.contarPalabras(textArea1.text, TextArea1.pos)
    '   Print "Numero de palabras: ", numeropalabras
    
    'mis Datos de la prueba
    misDatosPruebas.errores = SumandoErrores
    misDatosPruebas.palabrasleidas = numeropalabras
    
    If TiempoFinal <> 0 Then
      'se trata de una prueba con tiempo limite no superado.
      misDatosPruebas.tiempo = TiempoFinal
    Else
      'se trata de una preuba con tiempo limte agotado
      misDatosPruebas.tiempo = tiempoPruebaInicial
    Endif
    
    'mostrar informacion sobre el resultado....
    FormResultados.PersonaVOtmp = PersonaVOtmp
    FormResultados.DatosPruebas = misDatosPruebas
    FormResultados.FicheroLectura = ficheroNombreLectura
    FormResultados.ShowDialog()
    
    ''TODO: habria que preguntar por si graba los resultados de la prueba,,,,
    
  Endif
  
End

Public Sub ButtonSalir_Click()
  
  Me.Close()
  
End

Public Sub ButtonReiniciarPrueba_Click()
  
  ''TODO: repetir la prueba....
  SumandoErrores = 0
  iniciaFormulario()
  
End

Public Sub PanelPrincipal_MouseDown()
  
End
